#!/usr/bin/env -S awk -f

# consistently replace numbers to make golden tests more stable. Currently handles:
#   arg:NNN
#   {xyz:NNN (for any identifier `xyz`)
#   conArg:NNN
#   $resolvedNNN
#   ttc/NNNNNNNNNN
#   Foo.Bar:NN:NN--NN:NN
#   P:xyz:NNNNN (for any identifier `xyz`)
# Based on one authored by Steve Dunham, https://github.com/idris-lang/Idris2/pull/3156
# Used with his permission.

{
    out = ""
    # the last one is FC
    while (match($0, /(P:[A-z]+:|arg:|conArg:|ttc[\\\/][0-9]+|[$]resolved|{[A-z][A-z_0-9]*:)[0-9]+|[A-z.]+:[0-9]+:[0-9]+--[0-9]+:[0-9]+|[A-z]+[.][0-9]+:[0-9]+/)) {
        rs = RSTART
        rl = RLENGTH
        m = substr($0, rs, rl)
        pfx = "XXX"
        if (match(m,/^(\$resolved|arg:|conArg:|ttc[\\\/]|P:[A-z]+:|{[A-z][A-z_0-9]*:|[A-z.]+:|[A-z]+[.])/)) { pfx = substr(m, RSTART, RLENGTH) }
        if (!(m in mapping)) {
            # scope the count to the prefix so we can add more without breaking tests
            if (!count[pfx]) { count[pfx] = 1 }
            mapping[m] = count[pfx]
            count[pfx]++
        }
        out = out substr($0, 1, rs - 1) pfx mapping[m]
        $0 = substr($0, rs + rl)
    }
    print out $0
}

